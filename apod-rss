#!/usr/bin/perl -w
use strict;

use XML::RSS::SimpleGen;

my $ARCHIVE_URL  = 'http://antwrp.gsfc.nasa.gov/apod/archivepix.html';
my $BASE_URL     = 'http://antwrp.gsfc.nasa.gov/apod/';
my $RECENT_LIMIT = 12;
my $OUTPUT_FILE  = shift || 'public_html/apod.rss';

my $apod_archive = get_url($ARCHIVE_URL);

my @recent;
while ($apod_archive =~ m{(\d{4} [A-Za-z]+ \d{2}):\s+<a href="([\w\.]+)">([^<]+)</a>}g) {
    my ($date, $url, $title) = ($1, $2, $3);
    push @recent, {
        url   => $url,
        title => $title,
        date  => $date,
    };
    last if scalar @recent >= $RECENT_LIMIT;
}

rss_new($BASE_URL, 'APOD', 'Astronomy Picture of the Day');
rss_language('en');
rss_webMaster('peichman@chesapeake.net');
rss_daily;
rss_updateHours(0);

for my $item (@recent) {
    rss_item($BASE_URL . $item->{url}, sprintf('%s (%s)', $item->{title}, $item->{date}));
}

rss_save($OUTPUT_FILE);
